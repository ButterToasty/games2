//Animated shader using vertex texture

attribute highp float aVertexId;
attribute vec2 aVertexTexCoord;
uniform sampler2D uBlendSampler;
uniform vec2 uTextureDims;
uniform vec2 uVTFrames; //frame and nextframe id

uniform mat4 uVPMatrix;
uniform mat4 uVMatrix;
uniform mat3 uNMatrix;

uniform mediump vec3 uLightPos;
uniform mediump vec3 uLightColor;
uniform vec3 uFillPos;
uniform mediump vec3 uFillColor;

uniform vec2 uRotationSinCos;
uniform vec3 uModelPos;
uniform float uAnimInterp;

varying vec3 vDiffuse;
varying vec3 vFill;
varying vec2 texCoord;
varying vec3 vModelPos;

varying vec3 vNorm;
varying vec3 vTransformedNorm;
varying vec4 vScreenPos;

uniform mat4 uShadowMVPMatrix;
varying vec3 vShadowPos;

void main(void) {
	
	float scale = sqrt( dot(uRotationSinCos, uRotationSinCos));//BEST TRICK EVER?
	
	float moment = (aVertexId+0.5)/uTextureDims.x;
	vec3 vert1 = texture2D( uBlendSampler , vec2(moment, (uVTFrames[0]*2.+0.5)/uTextureDims.y )).rgb;
	vec3 vert2 = texture2D( uBlendSampler , vec2(moment, (uVTFrames[1]*2.+0.5)/uTextureDims.y )).rgb;
	vec3 vertpos = (vert1*(1. - uAnimInterp) + vert2*uAnimInterp);
	
	vertpos = vec3( vertpos.x * uRotationSinCos.y - vertpos.y * uRotationSinCos.x, vertpos.x * uRotationSinCos.x + vertpos.y * uRotationSinCos.y , vertpos.z * scale );
	vec4 mPos =  vec4(uModelPos + vertpos,1.);
	gl_Position = uVPMatrix * mPos;
	
	vec3 norm1 = texture2D( uBlendSampler , vec2(moment, (uVTFrames[0]*2.+1.5)/uTextureDims.y )).rgb;
	vec3 norm2 = texture2D( uBlendSampler , vec2(moment, (uVTFrames[1]*2.+1.5)/uTextureDims.y )).rgb;
	vec3 interpNorm = (norm1*(1. - uAnimInterp) + norm2*uAnimInterp);
	vec3 vertNorm = vec3( interpNorm.x * uRotationSinCos.y - interpNorm.y * uRotationSinCos.x, interpNorm.x * uRotationSinCos.x + interpNorm.y * uRotationSinCos.y , interpNorm.z );
	
	vec3 transformedNormal = normalize(uNMatrix * vertNorm);

	vDiffuse = max(dot(transformedNormal, (uLightPos)), 0.0) *uLightColor;
	vNorm = vertNorm;
	vTransformedNorm = transformedNormal;
	MACRO_VFILL
	
	texCoord = aVertexTexCoord;
	vModelPos = mPos.xyz;
	vScreenPos = uVPMatrix * mPos;
	vec4 shadowClipPos = uShadowMVPMatrix * mPos;
	vShadowPos =  vec3( shadowClipPos.xyz / shadowClipPos.w)*0.5+0.5;
}